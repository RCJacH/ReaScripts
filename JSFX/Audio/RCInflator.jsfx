/*
JSFX Name: RCInflator
Author: RCJacH
Release Date: Aug 2021
Link:
  https://github.com/RCJacH/ReaScripts
  https://forum.cockos.com/showthread.php?t=256286
Version: 0.4
Reference:
  tviler
  Sony Oxford
  samu4199
About:
  JSFX implementation of Sony Oxford Inflator algorithm,
  found on Gearspace.
Changelog:
  * v0.5.1 (2021-08-13)
    * Fix unsymmetrical waveshaping
  * v0.5 (2021-08-13)
    + Refactor code
    + Add absolute clipping at 6dB.
    + Add waveshapeOvershoot
  * v0.4 (2021-08-13)
    + Accurate decibel to float conversion.
    + Wrap waveshaper above 0dB.
    + Refactor waveshaper into function.
  * v0.3 (2021-08-11)
    + Apply waveshaper symmetrically.
    * Fix sign Error.
  * v0.2 (2021-08-09)
    * Cleaned up code.
  * v0.1 (2021-02-06)
    + Initial alpha release.
*/

desc:RCInflator

slider1:0<-60, 60, 0.1>Input (dB)
slider2:100<0, 100, 0.1>Effect (%)
slider3:curve=0<-50, 50, 0.0001>Curve
slider4:clip=0<0,1,1{Off,On}>Clip
slider5:0<-60, 6, 0.1>Output (dB)

@init
function waveshape(in)
local(s1, s2, s3)
global(curveA, curveB, curveC, curveD)
(
  s1 = abs(in);
  s2 = s1 * s1;
  s3 = s2 * s1;

  curveA * s1 + curveB * s2 + curveC * s3 - curveD * (s2 - 2 * s3 + s2 * s2)
);

function waveshapeOvershoot(in)
local(s)
(
  s = min(max(in, -2), 2);

  2 * abs(s) - s * s
);

function process(in)
local(s, abs_s, sgn)
global(clip, dry, in_db, out_db, wet)
(
  s = in * in_db;
  sgn = sign(s);
  s = clip ? (
    waveshape(min(max(s, -1), 1));
  ) : (
    s > 1 || s < -1 ? waveshapeOvershoot(s) : waveshape(s);
  );
  s *= out_db;

  (sgn * s * wet + in * dry)
);

@slider
in_db = exp(0.11512925464970229 * slider1);
wet = slider2 * 0.01;
dry = 1 - wet;
out_db = exp(0.11512925464970229 * slider5);

curvepct = curve * 0.01;
// 1 + (curve + 50) / 100
curveA = 1.5 + curvepct;
// - curve / 50
curveB = -(curvepct + curvepct);
// (curve - 50) / 100
curveC = curvepct - 0.5;
// 1 / 16 - curve / 400 + curve ^ 2 / (4 * 10 ^ 4)
curveD = 0.0625 - curve * 0.0025 + (curve * curve) * 0.000025;

@sample

spl0 = process(spl0);
spl1 = process(spl1);
